set(PROJECT_SOURCE_DIR @PROJECT_SOURCE_DIR@)

execute_process(COMMAND @QTROOT@/bin/windeployqt.exe --verbose 9 --qmldir @SOURCE_ROOT@/src/ui ${CMAKE_INSTALL_PREFIX}/JellyfinMediaPlayer.exe)

# on windows windeployqt doesnt install non qt libraries, let cmake do that
include(BundleUtilities)

foreach(d @CMAKE_FIND_ROOT_PATH@)
  list(APPEND SEARCHDIRS ${d}/bin)
  list(APPEND SEARCHDIRS ${d}/lib)
endforeach()

get_filename_component(CEC_DIR @CEC_LIBRARY@ DIRECTORY)
list(APPEND SEARCHDIRS ${CEC_DIR})

fixup_bundle("${CMAKE_INSTALL_PREFIX}/JellyfinMediaPlayer.exe" "" "${SEARCHDIRS}") 
#install(PROGRAMS ${VCREDIST_DIR}/vc_redist_2015_x64.exe DESTINATION .)

if(@DO_ENABLE_ANGLE_DEP@)
  message(STATUS "Deploying separate ANGLE deps")
  file(INSTALL @DEPENDENCY_CACHE_DIR@/libEGL.dll @DEPENDENCY_CACHE_DIR@/libGLESv2.dll @DEPENDENCY_CACHE_DIR@/d3dcompiler_47.dll DESTINATION ${CMAKE_INSTALL_PREFIX})
else()
  message(STATUS "Not using separate ANGLE deps")
endif()

if(@DO_SIGN@)
  file(GLOB_RECURSE EXES ${CMAKE_INSTALL_PREFIX}/*.exe ${CMAKE_INSTALL_PREFIX}/*.dll)
  set(ENV{errorlevel} 1)
  foreach(e ${EXES})
    message("-- Signing: ${PROJECT_SOURCE_DIR}/scripts/WindowsSign.cmd ${e}")
    execute_process(COMMAND ${PROJECT_SOURCE_DIR}/scripts/WindowsSign.cmd "${e}" RESULT_VARIABLE RES)
  endforeach()
endif()
